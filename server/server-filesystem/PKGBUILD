# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgname=server-filesystem
pkgver=0.1.0
pkgrel=0
pkgdesc='Base RonixOS Server files'
arch=('x86_64')
license=('GPL')
url='https://ronixos.github.io'
groups=('base')
depends=('iana-etc')
replaces=(filesystem)
provides=(filesystem)
conflicts=(filesystem)
backup=('etc/crypttab' 'etc/fstab' 'etc/group' 'etc/gshadow' 'etc/host.conf'
  'etc/hosts' 'etc/issue' 'etc/ld.so.conf' 'etc/motd' 'etc/nsswitch.conf'
  'etc/passwd' 'etc/profile' 'etc/resolv.conf' 'etc/securetty'
  'etc/shadow' 'etc/shells' 'etc/hostname')
source=('crypttab' 'env-generator' 'fstab' 'group' 'gshadow' 'host.conf' 'hosts'
  'issue' 'ld.so.conf' 'locale.sh' 'motd' 'nsswitch.conf' 'os-release'
  'passwd' 'profile' 'resolv.conf' 'securetty' 'shadow' 'shells' 'sysctl'
  'sysusers' 'tmpfiles' 'ronixos.png')
md5sums=('5fa6674df7645d7f5895f2d12b4ef4e9'
  '2b0344e9639f35f3c0d5637a23556089'
  'e33f6dfdd61978fcb3ddf1431286e05a'
  '7fed1e1fb855e41a6d64d41f8521d69a'
  '1c1e3b08acfa286f4b417c49de3e4366'
  '7d119a9cce152aa182fb3392ddeecea7'
  'a1315ea3e2b64d197b6efaf9c14ff778'
  '420dcdc7fcab5a5771fd8f27e2ccf9bd'
  '5deb9f890a4d08a245e9752ede77271e'
  '6157d6b5893cf2f989ab800b4e418c45'
  'd41d8cd98f00b204e9800998ecf8427e'
  '981ee4d7e506a9d6d9ed2d872e16004b'
  '41c31bdc442ae8b3ea7f1d229900b3a2'
  '24f0f91e0dfb052bc5d307612f4e51b6'
  '447c0bbf309823a6fe6702daa0bf70f2'
  '0ee015fad07732676d9488ae498eed41'
  'f04bcb2803afc4dcb95670fe87343b4d'
  'f64466dd77c7bec37a8b47681468211a'
  '487ac898bd376eccb861f949001d16bb'
  'a51847c012555c843dbdf8df0da171d3'
  '6a3494296774369029c644c3e66daa48'
  '0267a3a463f35eec8a31f40a720dfd86'
  '553a478a4baed71000ec08c773be2196')

package() {
  cd "$pkgdir"

  # setup root filesystem
  for d in boot dev etc home mnt usr var opt srv/http run; do
    install -d -m755 $d
  done
  install -d -m555 proc
  install -d -m555 sys
  install -d -m0750 root
  install -d -m1777 tmp
  # vsftpd won't run with write perms on /srv/ftp
  # ftp (uid 14/gid 11)
  install -d -m555 -g 11 srv/ftp

  # setup /etc and /usr/share/factory/etc
  install -d etc/{ld.so.conf.d,skel,profile.d} usr/share/factory/etc
  for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf \
  passwd resolv.conf securetty shells profile; do
    install -m644 "$srcdir"/$f etc/
    install -m644 "$srcdir"/$f usr/share/factory/etc/
  done
  ln -s ../proc/self/mounts etc/mtab
  for f in gshadow shadow crypttab; do
    install -m600 "$srcdir"/$f etc/
    install -m600 "$srcdir"/$f usr/share/factory/etc/
  done
  install -m644 "$srcdir"/locale.sh etc/profile.d/locale.sh
  install -Dm644 "$srcdir"/os-release usr/lib/os-release

  # setup /var
  for d in cache local opt log/old lib/misc empty; do
    install -d -m755 var/$d
  done
  install -d -m1777 var/{tmp,spool/mail}

  # allow setgid games (gid 50) to write scores
  install -d -m775 -g 50 var/games
  ln -s spool/mail var/mail
  ln -s ../run var/run
  ln -s ../run/lock var/lock

  # setup /usr hierarchy
  for d in bin include lib share/{misc,pixmaps} src; do
    install -d -m755 usr/$d
  done
  for d in {1..8}; do
    install -d -m755 usr/share/man/man$d
  done

  # add lib symlinks
  ln -s usr/lib lib
  [[ $CARCH = 'x86_64' ]] && {
    ln -s usr/lib lib64
    ln -s lib usr/lib64
  }

  # add bin symlinks
  ln -s usr/bin bin
  ln -s usr/bin sbin
  ln -s bin usr/sbin

  # setup /usr/local hierarchy
  for d in bin etc games include lib man sbin share src; do
    install -d -m755 usr/local/$d
  done
  ln -s ../man usr/local/share/man

  # setup systemd-sysctl
  install -D -m644 "$srcdir"/sysctl usr/lib/sysctl.d/10-ronix.conf

  # setup systemd-sysusers
  install -D -m644 "$srcdir"/sysusers usr/lib/sysusers.d/ronix.conf

  # setup systemd-tmpfiles
  install -D -m644 "$srcdir"/tmpfiles usr/lib/tmpfiles.d/ronix.conf

  # setup systemd.environment-generator
  install -D -m755 "$srcdir"/env-generator usr/lib/systemd/system-environment-generators/10-ronix

  # add logo
  install -D -m644 "$srcdir"/ronixos.png usr/share/pixmaps

  echo "ronixos" > etc/hostname
}

# vim:set ts=2 sw=2 et:

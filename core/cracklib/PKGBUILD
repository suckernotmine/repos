# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=cracklib
pkgname=(cracklib cracklib-dev)
pkgver=2.9.7
pkgrel=1
pkgdesc="Password Checking Library"
arch=('x86_64')
license=('GPL')
url="https://github.com/cracklib/cracklib"
depends=('glibc' 'zlib')
source=(https://github.com/cracklib/cracklib/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('48a0c8810ec4780b99c0a4f9931c21c6')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --without-python
  make 
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package_cracklib() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/usr/share/dict
  ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
  mv "$pkgdir/usr/include" "$srcdir/include"
}

package_cracklib-dev() {
  pkgdesc+=" (development files)"
  depends=('cracklib')
  mkdir -p "$pkgdir/usr"
  mv "$srcdir/include" "$pkgdir/usr/include"
}

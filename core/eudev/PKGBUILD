# Maintainer: Tristan Ross <spaceboyross@yandex.com>

_udev_ver=237

pkgbase=eudev
pkgname=('eudev' 'libeudev' 'eudev-dev' 'libeudev-dev')
pkgver=3.2.5
pkgrel=0
arch=('x86_64')
url="http://www.gentoo.org/proj/en/eudev/"
license=('GPL')
makedepends=('gobject-introspection' 'gperf' 'gtk-doc' 'intltool' 'kmod')
options=('!libtool' '!staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gentoo/eudev/archive/v${pkgver}.tar.gz"
  'udev-hwdb.hook')
sha256sums=('870920011285b919fca5c60eaa5670312eeffe3883dd8cf564cd1179fc639336'
  '846e9ddbb95c8394ba7efe75107cc1308426921bc042f5d6b48fa4c2dcbac151')

prepare(){
	cd "${srcdir}/${pkgbase}-${pkgver}"
	sed -e 's/GROUP="dialout"/GROUP="uucp"/' \
		-e 's/GROUP="tape"/GROUP="storage"/' \
		-e 's/GROUP="cdrom"/GROUP="optical"/' \
		-i rules/*.rules
}

groups=('base' 'base-openrc')

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-rootprefix=/usr \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-introspection \
		--enable-kmod \
		--enable-manpages \
		--enable-split-usr

	make
}

package_eudev() {
	pkgdesc="The userspace dev tools (udev) forked by Gentoo"
	provides=("udev=${_udev_ver}")
	depends=("libeudev" 'kbd' 'kmod' 'hwids' 'util-linux')
	backup=('etc/udev/udev.conf')

	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	rm -rv "$pkgdir"/usr/lib/pkgconfig
	rm -v "$pkgdir"/usr/include/libudev.h
	rm -v "$pkgdir"/usr/lib/libudev*.{so*,a}

	install -Dm644 "$srcdir/udev-hwdb.hook" "$pkgdir/usr/share/libalpm/hooks/udev-hwdb.hook"

  # dev split
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/share/pkgconfig/" "$srcdir/pkgconfig"
}

package_libeudev() {
	pkgdesc="eudev client libraries"
	depends=('glib2' 'glibc')
	conflicts=('libudev.so')
	provides=("libudev=${_udev_ver}" 'libudev.so')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C src/libudev DESTDIR="$pkgdir" install

  # dev split
  mv "$pkgdir/usr/include" "$srcdir/libinclude"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/libpkgconfig"
}

package_eudev-dev() {
  pkgdesc="The userspace dev tools (udev) forked by Gentoo (development files)"
  depends=(eudev)

  mkdir -p "$pkgdir/usr/lib"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/share/pkgconfig/"
}

package_libeudev-dev() {
  pkgdesc="eudev client libraries (development files)"
  depends=(libeudev)

  mkdir -p "$pkgdir/usr/lib"
  mv "$srcdir/libinclude" "$pkgdir/usr/include"
  mv "$srcdir/libpkgconfig" "$pkgdir/usr/lib/pkgconfig/"
}

# vim:set ts=2 sw=2 et:

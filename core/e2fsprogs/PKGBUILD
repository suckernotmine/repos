# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=e2fsprogs
pkgname=(e2fsprogs e2fsprogs-dev)
pkgver=1.45.2
pkgrel=0
pkgdesc="Ext2/3/4 filesystem utilities"
arch=('x86_64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh' 'libutil-linux')
makedepends=('util-linux')
backup=('etc/mke2fs.conf')
options=('staticlibs')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0') # Theodore Ts'o <tytso@mit.edu>
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar."{xz,sign})
sha256sums=('4952c9ae91e36d762e13cc5b9e8f7eeb5453e4aee4cd9b7402e73f2d4e65e009' 'SKIP')


prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
      --prefix=/usr \
      --with-root-prefix="" \
      --libdir=/usr/lib \
      --sbindir=/usr/bin \
      --enable-elf-shlibs \
      --disable-fsck \
      --disable-uuidd \
      --disable-libuuid \
      --disable-libblkid

  make
}

package_e2fsprogs() {
  unset MAKEFLAGS

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-libs

  sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

  # remove references to build directory
  sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
  sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"

  # remove static libraries with a shared counterpart
  rm "${pkgdir}"/usr/lib/lib{com_err,e2p,ext2fs,ss}.a

  rm -rf "$pkgdir/usr/lib/systemd/"

  # dev split
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/pkgconfig"
  mv "$pkgdir/usr/share/man/man3/" "$srcdir/man3"
}

package_e2fsprogs-dev() {
  pkgdesc+=" (development files)"
  depends=(e2fsprogs)

  mkdir -p "$pkgdir/usr/share/man" "$pkgdir/usr/lib"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig/"
  mv "$srcdir/man3" "$pkgdir/usr/share/man/man3"
}

# vim:set ts=2 sw=2 et:

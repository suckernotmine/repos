# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=acl
pkgname=(acl acl-dev)
pkgver=2.2.53
pkgrel=0
pkgdesc='Access control list utilities, libraries and headers'
arch=('x86_64')
url='https://savannah.nongnu.org/projects/acl'
license=('LGPL')
depends=('attr')
replaces=('xfsacl')
provides=('xfsacl')
conflicts=('xfsacl')
validpgpkeys=('5F5F7705EBC4BADC7A3007D11DCE2C057903C34D' # Tristan Ross <spaceboyross@yandex.com>
  'B902B5271325F892AC251AD441633B9FE837F581') # Mike Frysinger <vapier@gentoo.org>
source=("https://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('06be9865c6f418d851ff4494e12406568353b891ffe1f596b34693c387af26c7' 'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --prefix=/usr
  make
}

package_acl() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig" "$srcdir/pkgconfig"
  mv "$pkgdir/usr/share/man/man3/" "$srcdir/man3"
  mv "$pkgdir/usr/share/man/man5/" "$srcdir/man5"
}

package_acl-dev() {
  pkgdesc+=" (development files)"
  depends=('acl')

  cd "${pkgname/-dev/}-$pkgver"
  mkdir -p "$pkgdir/"usr/lib "$pkgdir/usr/share/man"
  mv "$srcdir/include" "$pkgdir/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig"
  mv "$srcdir/man"{3,5} "$pkgdir/usr/share/man/"
}

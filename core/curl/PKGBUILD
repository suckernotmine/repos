# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=curl
pkgname=(curl curl-dev)
pkgver=7.65.3
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libssh2' 'openssl' 'zlib' 'libpsl' 'libnghttp2'
         'libidn2' 'libidn2.so')
provides=('libcurl.so')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('df8fc6b2cccf100f7479e25cad743964a84066b587da19585b36a788b0041925e33944986d636a451d6bb95a452d5ac6812b2d5fa6631a10e0ac82a2c7821c75' 'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-ldap \
      --disable-ldaps \
      --disable-manual \
      --enable-ipv6 \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-libssh2 \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
      --without-librtmp \
      --without-brotli

  make
}

package_curl() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C scripts

  # license
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING

  mv "$pkgdir/usr/bin/curl-config" "$srcdir/curl-config"
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/pkgconfig"
  mv "$pkgdir/usr/share/man/man1/curl-config.1" "$srcdir/man-curl-config.1"
  mv "$pkgdir/usr/share/man/man3/" "$srcdir/man3"
}

package_curl-dev() {
  pkgdesc+=" (development files)"
  depends=(curl)

  mkdir -p "$pkgdir/usr/share/man/man1" "$pkgdir/usr/lib/" "$pkgdir/usr/bin"
  mv "$srcdir/curl-config" "$pkgdir/usr/bin/curl-config"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig/"
  mv "$srcdir/man-curl-config.1" "$pkgdir/usr/share/man/man1/curl-config.1"
  mv "$srcdir/man3" "$pkgdir/usr/share/man/man3"
}

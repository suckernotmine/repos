# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=expat
pkgname=(expat expat-dev)
pkgver=2.2.6
pkgrel=0
pkgdesc='An XML parser library'
arch=(x86_64)
url='https://libexpat.github.io/'
license=(custom)
depends=(glibc)
source=(https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2{,.asc})
validpgpkeys=(3D7E959D89FACFEE38371921B00BC66A401A1600) # Sebastian Pipping
sha256sums=('17b43c2716d521369f82fc2dc70f359860e90fa440bea65b3b85f0b246ea81f2' 'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package_expat() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "-t$pkgdir/usr/share/licenses/$pkgname" COPYING

  # dev split
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/pkgconfig"
}

package_expat-dev() {
  pkgdesc+=" (development files)"
  depends=(expat)

  mkdir -p "$pkgdir/usr/lib/"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig/"
}

# vim:set ts=2 sw=2 et:

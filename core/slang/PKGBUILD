# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgname=slang
pkgver=2.3.2
pkgrel=0
pkgdesc="S-Lang is a powerful interpreted language"
arch=('x86_64')
url="https://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(https://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha1sums=('bbf7f2dcc14e7c7fca40868fd4b411a2bd9e2655' 'SKIP')
validpgpkeys=('AE962A02D29BFE4A4BB2805FDE401E0D5873000A')  # John E. Davis

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH = "i686" ]] || make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}

# vim:set ts=2 sw=2 et:

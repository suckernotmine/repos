# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase=bash
pkgname=(bash bash-dev)
_basever=5.0
_patchlevel=007
pkgver=${_basever}.${_patchlevel}
pkgrel=0
pkgdesc='The GNU Bourne Again shell'
arch=(x86_64)
license=(GPL)
url='https://www.gnu.org/software/bash/bash.html'
groups=(base)
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=7.0' glibc ncurses)
provides=('sh')
source=(https://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
  dot.bashrc
  dot.bash_profile
  dot.bash_logout
  system.bashrc
  system.bash_logout)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB' # Chet Ramey
  '5F5F7705EBC4BADC7A3007D11DCE2C057903C34D') # Tristan Ross <spaceboyross@yandex.com>

if [[ $((10#${_patchlevel})) -gt 0 ]]; then
  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    source=(${source[@]} https://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//.}-$(printf "%03d" $_p){,.sig})
  done
fi

prepare() {
  cd $pkgname-$_basever

  for (( _p=1; _p<=$((10#${_patchlevel})); _p++ )); do
    msg "applying patch bash${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../bash${_basever//.}-$(printf "%03d" $_p)
  done
}

build() {
  cd $pkgname-$_basever

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
    -DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
    -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
    -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
    -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline
  make
}

check() {
  make -C $pkgname-$_basever check
}

package_bash() {
  optdepends=('bash-completion: for tab completion')
  make -C $pkgname-$_basever DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/pkgconfig"
  ln -s bash "$pkgdir/usr/bin/sh"

  # system-wide configuration files
  install -Dm644 system.bashrc "$pkgdir/etc/bash.bashrc"
  install -Dm644 system.bash_logout "$pkgdir/etc/bash.bash_logout"

  # user configuration file skeletons
  install -dm755 "$pkgdir/etc/skel/"
  install -m644 dot.bashrc "$pkgdir/etc/skel/.bashrc"
  install -m644 dot.bash_profile "$pkgdir/etc/skel/.bash_profile"
  install -m644 dot.bash_logout "$pkgdir/etc/skel/.bash_logout"
}

package_bash-dev() {
  pkgdesc+=" (developmental files)"
  depends=('bash')
  mkdir -p "$pkgdir/usr/lib"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig/"
}

md5sums=('2b44b47b905be16f45709648f671820b'
         'SKIP'
         '027d6bd8f5f6a06b75bb7698cb478089'
         '2902e0fee7a9168f3a4fd2ccd60ff047'
         '42f4400ed2314bd7519c020d0187edc5'
         'd8f3f334e72c0e30032eae1a1229aef1'
         '472f536d7c9e8250dc4568ec4cfaf294'
         'b026862ab596a5883bb4f0d1077a3819'
         'SKIP'
         '2f4a7787365790ae57f36b311701ea7e'
         'SKIP'
         'af7f2dd93fd5429fb5e9a642ff74f87d'
         'SKIP'
         'b60545b273bfa4e00a760f2c648bed9c'
         'SKIP'
         '875a0bedf48b74e453e3997c84b5d8a4'
         'SKIP'
         '4a8ee95adb72c3aba03d9e8c9f96ece6'
         'SKIP'
         '411560d81fde2dc5b17b83c3f3b58c6f'
         'SKIP')

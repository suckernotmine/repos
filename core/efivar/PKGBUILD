# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgbase="efivar"
pkgname=(efivar efivar-dev)
pkgdesc="Tools and library to manipulate EFI variables"
pkgver=37
pkgrel=0
arch=('x86_64')
url="https://github.com/rhinstaller/efivar"
license=('LGPL2.1')
makedepends=('git')
depends=('popt')
conflicts=('libefivar')
provides=("libefivar=${pkgver}")
options=('strip' 'zipman' 'docs')
source=("efivar::git+https://github.com/rhinstaller/efivar.git#tag=${pkgver}")
sha1sums=('SKIP')

pkgver() {
	cd "${srcdir}/efivar/"
	echo "$(git describe --tags)" | sed -e 's|efivar-||g' -e 's|-|\.|g'
}

prepare() {
	cd "${srcdir}/efivar/"
	
	git clean -x -d -f
	
	sed 's|-O0|-Os|g' -i "${srcdir}/efivar/Make.defaults" || true
	sed 's|-rpath,$(TOPDIR)/src|-rpath,$(libdir)|g' -i "${srcdir}/efivar/src/test/Makefile" || true
}

build() {
	cd "${srcdir}/efivar/"
	CFLAGS="${CFLAGS} -flto -Wno-address-of-packed-member"

	make libdir="/usr/lib/" bindir="/usr/bin/" mandir="/usr/share/man/" includedir="/usr/include/" V=1 -j1
  cd "${srcdir}/efivar/src/test"
  make libdir="/usr/lib/" bindir="/usr/bin/" mandir="/usr/share/man/" includedir="/usr/include/" V=1 -j1
}

package_efivar() {
	cd "${srcdir}/efivar/"
	
	make -j1 V=1 DESTDIR="${pkgdir}/" libdir="/usr/lib/" bindir="/usr/bin/" mandir="/usr/share/man/" includedir="/usr/include/" install
	
	install -d "${pkgdir}/usr/bin"
	install -D -m0755 "${srcdir}/efivar/src/test/tester" "${pkgdir}/usr/bin/efivar-tester"

  # dev split
  mv "$pkgdir/usr/include" "$srcdir/include"
  mv "$pkgdir/usr/lib/pkgconfig/" "$srcdir/pkgconfig"
  mv "$pkgdir/usr/share/man/man3/" "$srcdir/man3"
}

package_efivar-dev() {
  pkgdesc+=" (development files)"
  depends=(efivar)

  mkdir -p "$pkgdir/usr/share/man" "$pkgdir/usr/lib"
  mv "$srcdir/include" "$pkgdir/usr/include"
  mv "$srcdir/pkgconfig" "$pkgdir/usr/lib/pkgconfig/"
  mv "$srcdir/man3" "$pkgdir/usr/share/man/man3"
}

# vim:set ts=2 sw=2 et:

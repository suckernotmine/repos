# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgname=xfsprogs
pkgver=4.20.0
pkgrel=0
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="https://xfs.org"
depends=('sh' 'libutil-linux' 'readline')
makedepends=('git')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}-dev"
  make configure
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-readline
  make
}

package() {
  cd "${srcdir}/${pkgname}-dev"
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}

# vim:set ts=2 sw=2 et:

# Maintainer: Tristan Ross <spaceboyross@yandex.com>

_pkgname=polkit
pkgname=polkit
pkgver=0.115+41+g941e932
pkgrel=1
pkgdesc="PolyciKit with elogind support for non-systemd systems"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/polkit"
license=('LGPL')
provides=("polkit=$pkgver")
depends=('glib2' 'pam' 'expat' 'js60' 'libelogind' 'libutil-linux')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git' 'autoconf-archive')
conflicts=('polkit' 'polkit-consolekit')
# replaces=('polkit-consolekit')
options=('!libtool')
install=polkit.install
_commit=941e9329f8d3d43ace8335d206365b212824e686
#source=("http://www.freedesktop.org/software/polkit/releases/$_pkgname-$pkgver.tar.gz")
source=("git+https://anongit.freedesktop.org/git/polkit#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname #-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname #-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libexecdir=/usr/lib/polkit-1 \
    --enable-libsystemd-login=no \
    --enable-libelogind=yes \
    --disable-static \
    --enable-gtk-doc \
    --with-authfw=pam \
    --with-os-type=redhat
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $_pkgname #-$pkgver
  make -k check || :
}

package() {
  cd $_pkgname #-$pkgver
  make DESTDIR="$pkgdir" install
  chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  chmod 750 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
}

# vim:set ts=2 sw=2 et:

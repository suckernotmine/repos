# Maintainer: Tristan Ross <spaceboyross@yandex.com>

pkgname=nbd
pkgver=3.19
pkgrel=0
pkgdesc='tools for network block devices, allowing you to use remote block devices over TCP/IP'
arch=('x86_64')
url='https://nbd.sourceforge.io/'
license=('GPL')
depends=('glib2' 'gnutls')
backup=('etc/nbd-server/config')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
  'config'
  'nbd.sv.run'
  'sysusers_nbd.conf')
sha256sums=('b4466412f13e057659f25d35e1e8e181afd62c7179bff22a6add81445ecb8690'
  'ee2e9fbbeb8a8b9b71d16b6f32eb41788f6def9d00cc4a47897ed3cb97cdde7c'
  '252954d425e1fe3dee33f8f430c30465eb84764a8dec93aa2ce80ea79a0c15b2'
  '3fd98fe87d0b31b82126dc669f00355865f58fae81af3fccbc0d06504baef6cd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-syslog
  make
}

# checks still fail...
#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#
#  make check
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m0644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m0644 "${srcdir}"/nbd.sv.run "${pkgdir}"/etc/sv/nbd/run
  install -D -m0644 "${srcdir}"/sysusers_nbd.conf "${pkgdir}"/usr/lib/sysusers.d/nbd.conf

  install -D -m0644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}

# vim:set ts=2 sw=2 et:
